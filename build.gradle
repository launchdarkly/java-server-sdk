apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'io.codearte.nexus-staging'

configurations.all {
    // check for updates every build for dependencies with: 'changing: true'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

ext.libraries = [:]

ext.imports = [
    commons_codec: [ version: "1.10" ],
    guava: [ version: "19.0" ],
    joda_time: [ version: "2.9.3" ],
    okhttp_eventsource: [ version: "1.7.1" ],
    snakeyaml: [ version: "1.19" ],
    jedis: [ version: "2.9.0" ],
    gson: [ version: "2.7", limit: "3" ],
    slf4j_api: [ version: "1.7.21", limit: "2" ]
]

libraries.internal = [
    "commons-codec:commons-codec:" + imports.commons_codec.version,
    "com.google.guava:guava:" + imports.guava.version,
    "joda-time:joda-time:" + imports.joda_time.version,
    "com.launchdarkly:okhttp-eventsource:" + imports.okhttp_eventsource.version,
    "org.yaml:snakeyaml:" + imports.snakeyaml.version,
    "redis.clients:jedis:" + imports.jedis.version
]

libraries.external = [
    "com.google.code.gson:gson:" + imports.gson.version,
    "org.slf4j:slf4j-api:" + imports.slf4j_api.version
]

libraries.test = [
    "com.squareup.okhttp3:mockwebserver:3.10.0",
    "org.hamcrest:hamcrest-all:1.3",
    "org.easymock:easymock:3.4",
    "junit:junit:4.12",
    "ch.qos.logback:logback-classic:1.1.7"
]

dependencies {
    implementation libraries.internal
    compileClasspath libraries.external
    runtime libraries.internal, libraries.external
    testImplementation libraries.test, libraries.internal, libraries.external
    shadow libraries.external
}

jar {
    baseName = 'launchdarkly-client'
    // thin classifier means that the non-shaded non-fat jar is still available
    // but is opt-in since users will have to specify it.
    classifier = 'thin'
    manifest {
        attributes("Implementation-Version": version)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.5.0-rc.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

githubPages {
    repoUri = 'https://github.com/launchdarkly/java-client.git'
    pages {
        from javadoc
    }
    credentials {
        username = githubUser
        password = githubPassword
    }
}

shadowJar {
    baseName = 'launchdarkly-client'
    //no classifier means that the shaded jar becomes the default artifact
    classifier = ''

    // Don't shade or include slf4j
    dependencies{
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('com.google.code.gson:.*:.*'))
    }

    // Shade all jars except for launchdarkly
    relocate('com', 'com.launchdarkly.shaded.com') {
        exclude("com.launchdarkly.client.*")
        exclude("com.launchdarkly.client.files.*")
        exclude("com.google.gson.*")
        exclude("com.google.gson.annotations.*")
        exclude("com.google.gson.internal.*")
        exclude("com.google.gson.internal.bind.*")
        exclude("com.google.gson.internal.bind.util.*")
        exclude("com.google.gson.reflect.*")
        exclude("com.google.gson.stream.*")
    }
    relocate('okhttp3', 'com.launchdarkly.shaded.okhttp3')
    relocate('okio', 'com.launchdarkly.shaded.okio')
    relocate('org', 'com.launchdarkly.shaded.org') {
        exclude("org.slf4j.*")
        exclude("org.slf4j.event.*")
        exclude("org.slf4j.helpers.*")
        exclude("org.slf4j.spi.*")
    }
    relocate('redis', 'com.launchdarkly.shaded.redis')

    manifest {
        attributes("Implementation-Version": version)

        attributes(osgiBaseAttributes())
        attributes(osgiImports())
        attributes("Export-Package": [
                bundleExport("com.launchdarkly.client"),
                bundleExport("com.launchdarkly.client.files")
            ].join(","))
    }
}

def osgiBaseAttributes() {
    [
        "Bundle-SymbolicName": "com.launchdarkly.client",
        "Bundle-Version": version,
        "Bundle-Name": "launchdarkly-client",
        "Bundle-ManifestVersion": "2"
    ]
}

def osgiImports() {
    [
        "Import-Package": [
            bundleImport("com.google.gson", imports.gson),
            bundleImport("com.google.gson.annotations", imports.gson),
            bundleImport("com.google.gson.reflect", imports.gson),
            bundleImport("com.google.gson.stream", imports.gson),
            bundleImport("org.slf4j", imports.slf4j_api)
        ].join(",")
    ]
}

def bundleExport(packageName) {
    packageName + ";version=\"" + version + "\""
}

def bundleImport(packageName, importDef) {
    packageName + ";version=\"[" + importDef.version + "," + importDef.limit + ")\""
}

// This builds the "-all"/"fat" jar which also includes the external dependencies - SLF4J,
// Gson, and javax.annotations - in unshaded form, as well as all the internal shaded classes.
task shadowJarAll(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    baseName = 'launchdarkly-client'
    classifier = 'all'
    group = "shadow"
    description = "Builds a Shaded fat jar including SLF4J"
    from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
    configurations = [project.configurations.runtime]
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')

    // Shade all jars except for launchdarkly
    relocate('com', 'com.launchdarkly.shaded.com') {
        exclude("com.launchdarkly.client.*")
        exclude("com.google.gson.*")
        exclude("com.google.gson.annotations.*")
        exclude("com.google.gson.internal.*")
        exclude("com.google.gson.internal.bind.*")
        exclude("com.google.gson.internal.bind.util.*")
        exclude("com.google.gson.reflect.*")
        exclude("com.google.gson.stream.*")
    }
    relocate('okhttp3', 'com.launchdarkly.shaded.okhttp3')
    relocate('okio', 'com.launchdarkly.shaded.okio')
    relocate('org', 'com.launchdarkly.shaded.org') {
        exclude("org.slf4j.*")
        exclude("org.slf4j.event.*")
        exclude("org.slf4j.helpers.*")
        exclude("org.slf4j.spi.*")
    }
    relocate('redis', 'com.launchdarkly.shaded.redis')

    manifest {
        attributes("Implementation-Version": version)
    }
}

artifacts {
    archives jar, sourcesJar, javadocJar, shadowJar, shadowJarAll
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        downloadJavadoc = true

        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
}

def pomConfig = {
    name 'LaunchDarkly SDK for Java'
    packaging 'jar'
    url 'https://github.com/launchdarkly/java-client'

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'jkodumal'
            name 'John Kodumal'
            email 'john@launchdarkly.com'
        }
    }

    scm {
        connection 'scm:git:git://github.com/launchdarkly/java-client.git'
        developerConnection 'scm:git:ssh:git@github.com:launchdarkly/java-client.git'
        url 'https://github.com/launchdarkly/java-client'
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifactId = 'launchdarkly-client'
            artifact jar
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJarAll
            
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Official LaunchDarkly SDK for Java')
                asNode().children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.shadow
}
